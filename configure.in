dnl Process this file with autoconf to produce a configure script.

dnl ---------------------------------------------------------------------------
dnl Versioning Information:
dnl     Increment Major more radical changes (ie re-rights)
dnl     Increment Minor more additional features which don't break things
dnl
dnl     Exception to this rule is the Major version 0, minor version will be
dnl     incremented until we are ready to say DVBStreamer is no longer alpha.
dnl ---------------------------------------------------------------------------
AC_INIT(dvbstreamer, 0.7)
DVBSTREAMER_MAJOR=0
DVBSTREAMER_MINOR=7

AC_SUBST(DVBSTREAMER_MAJOR)
AC_SUBST(DVBSTREAMER_MINOR)

AC_DEFINE_UNQUOTED(DVBSTREAMER_MAJOR, $DVBSTREAMER_MAJOR, [DVBStreamer Major version number])
AC_DEFINE_UNQUOTED(DVBSTREAMER_MINOR, $DVBSTREAMER_MINOR, [DVBStreamer Minor version number])
AC_DEFINE_UNQUOTED(DVBSTREAMER_VERSION, ((DVBSTREAMER_MAJOR<<16) | (DVBSTREAMER_MINOR)), [DVBStreamer combined version number as an int])

SPEC_VERSION=$DVBSTREAMER_MAJOR.$DVBSTREAMER_MINOR
AM_INIT_AUTOMAKE("dvbstreamer", $SPEC_VERSION)

AC_CONFIG_SUBDIRS(libltdl)
AM_CONFIG_HEADER(config.h)

AC_ISC_POSIX
AC_PROG_CC
AM_PROG_CC_STDC
AC_HEADER_STDC
AM_PROG_CC_C_O 

dnl ---------------------------------------------------------------------------
dnl Libltdl setup
dnl ---------------------------------------------------------------------------
AC_LIBTOOL_WIN32_DLL dnl Make sure under cygwin we can load plugins !!!
AC_LIBTOOL_DLOPEN
AC_LIBLTDL_CONVENIENCE
AC_PROG_LIBTOOL
AC_SUBST(INCLTDL)
AC_SUBST(LIBLTDL)

dnl ---------------------------------------------------------------------------
dnl Work out the libdir name (copied from xine-lib configure.ac)
dnl ---------------------------------------------------------------------------
AC_MSG_CHECKING([libdir name])
case $host in
*-*-linux*)
 # Test if the compiler is 64bit
 echo 'int i;' > conftest.$ac_ext
 DVBSTREAMER_cv_cc_64bit_output=no
 if AC_TRY_EVAL(ac_compile); then
 case `"$MAGIC_CMD" conftest.$ac_objext` in
 *"ELF 64"*)
   DVBSTREAMER_cv_cc_64bit_output=yes
   ;;
 esac
 fi
 rm -rf conftest*
 ;;
esac

case $host_cpu:$DVBSTREAMER_cv_cc_64bit_output in
powerpc64:yes | s390x:yes | sparc64:yes | x86_64:yes)
 DVBSTREAMER_LIBNAME="lib64"
 ;;
*:*)
 DVBSTREAMER_LIBNAME="lib"
 ;;
esac
AC_MSG_RESULT([$DVBSTREAMER_LIBNAME])

dnl ---------------------------------------------------------------------------
dnl Enable args
dnl ---------------------------------------------------------------------------
AC_ARG_ENABLE([atsc],
	AS_HELP_STRING([--enable-atsc], [ Build dvbstreamer with ATSC support. The name of the executable will be atscstreamer rather than dvbstreamer]),
     	[case "${enableval}" in
       	yes) atsc=true ;;
       	no)  atsc=false ;;
       	*) AC_MSG_ERROR([bad value ${enableval} for --enable-atsc]) ;;
    	esac],[atsc=false])

AM_CONDITIONAL([ATSC], [test x$atsc = xtrue])

AC_ARG_ENABLE([getaddrinfo],
	AS_HELP_STRING([--disable-getaddrinfo], [ Disable use of the getaddrinfo function, this is needed for IPv6 support but some libc don't properly support it]),
	[case "${enableval}" in
       	yes) use_getaddrinfo=true ;;
       	no)  use_getaddrinfo=false ;;
       	*) AC_MSG_ERROR([bad value ${enableval} for --disable-getaddrinfo]) ;;
     	esac],[use_getaddrinfo=true])

if test "${use_getaddrinfo}" != "false"; then
    AC_DEFINE(USE_GETADDRINFO, 1, Use getaddrinfo for address resolution)
fi

dnl ---------------------------------------------------------------------------
dnl Check for sqlite 3
dnl ---------------------------------------------------------------------------
AC_CHECK_LIB([sqlite3], [sqlite3_libversion], [], [AC_MSG_FAILURE([sqlite3 not found])])
LIBS=""

dnl ---------------------------------------------------------------------------
dnl Check for libreadline
dnl ---------------------------------------------------------------------------
AC_CHECK_HEADER([readline/readline.h], [], [AC_MSG_FAILURE([readline header file not found])])

AC_CHECK_LIB([readline], [readline], [READLINE_TERMCAP="-lncurses"],
    [AC_CHECK_LIB([readline], [readline], [READLINE_TERMCAP="-ltermcap"],
        [AC_MSG_FAILURE([readline not found])],
        [-ltermcap])],
    [-lncurses])

AC_SUBST(READLINE_TERMCAP)

LIBS=""

dnl ---------------------------------------------------------------------------
dnl Check whether librt is needed.
dnl ---------------------------------------------------------------------------
AC_SEARCH_LIBS(clock_gettime, [rt], [GETTIME_LIB=$LIBS], [AC_MSG_FAILURE([Failed to find a library defining clock_gettime])])

AC_SUBST(GETTIME_LIB)

LIBS=""

dnl ---------------------------------------------------------------------------
dnl Check for variadic macros
dnl ---------------------------------------------------------------------------
AC_CACHE_CHECK([for variadic cpp macros],
    [ac_cv_cpp_variadic_macros],
    [AC_TRY_COMPILE(
         [#include <stdio.h>
          #define a(b,c...) printf(b,##c)],
         [a("foo");a("%s","bar");a("%s%s","baz","quux");],
         ac_cv_cpp_variadic_macros=yes,
         ac_cv_cpp_variadic_macros=no)])
if test "${ac_cv_cpp_variadic_macros}" != "no"; then
    AC_DEFINE(HAVE_VARIADIC_MACROS, 1, Support for variadic macros)
fi

dnl ---------------------------------------------------------------------------
dnl Setup package directories
dnl ---------------------------------------------------------------------------

dnl Set PACKAGE SOURCE DIR in config.h.
packagesrcdir=`cd $srcdir && pwd`

dnl Set PACKAGE PREFIX
if test "x${prefix}" = "xNONE"; then
  packageprefix=${ac_default_prefix}
else
  packageprefix=${prefix}
fi

dnl Set PACKAGE DATA & DOC DIR
packagedatadir=share
packagedocdir=doc/${PACKAGE}

dnl Subst PACKAGE_DATA_DIR.
NO_PREFIX_PACKAGE_DATA_DIR="${packagedatadir}"
AC_SUBST(NO_PREFIX_PACKAGE_DATA_DIR)
PACKAGE_DATA_DIR="${packageprefix}/${packagedatadir}"
AC_SUBST(PACKAGE_DATA_DIR)

dnl Subst PACKAGE_DOC_DIR.
NO_PREFIX_PACKAGE_DOC_DIR="${packagedocdir}"
AC_SUBST(NO_PREFIX_PACKAGE_DOC_DIR)
PACKAGE_DOC_DIR="${packageprefix}/${packagedocdir}"
AC_SUBST(PACKAGE_DOC_DIR)

AC_DEFINE_UNQUOTED(PACKAGE_DATA_DIR, "${packageprefix}/${packagedatadir}", [Define this to data directory location])
AC_DEFINE_UNQUOTED(PACKAGE_DOC_DIR, "${packageprefix}/${packagedocdir}", [Define this to Docs directory location])
AC_DEFINE_UNQUOTED(PACKAGE_SOURCE_DIR, "${packagesrcdir}", [Define this to source directory location])

dnl ---------------------------------------------------------------------------
dnl Work out the Plugins path (Copied from xine-lib configure.ac)
dnl ---------------------------------------------------------------------------
if test "x$prefix" = xNONE; then
   prefix="${ac_default_prefix}"
fi
if test "x$exec_prefix" = xNONE; then
   exec_prefix='${prefix}'
fi

DVBSTREAMER_PLUGINDIR="$libdir/dvbstreamer/plugins"

eval DVBSTREAMER_REL_PLUGINDIR="$DVBSTREAMER_PLUGINDIR"
DVBSTREAMER_REL_PLUGINDIR=`eval echo "$DVBSTREAMER_REL_PLUGINDIR" | sed -e "s,^${prefix}/,,"`

eval DVBSTREAMER_PLUGINPATH=`eval echo "$DVBSTREAMER_PLUGINDIR"`
AC_DEFINE_UNQUOTED(DVBSTREAMER_PLUGINDIR,"$DVBSTREAMER_PLUGINPATH",[Define this to plugins directory location])
AC_DEFINE_UNQUOTED(DVBSTREAMER_REL_PLUGINDIR,"$DVBSTREAMER_REL_PLUGINDIR",[Define this to plugin directory relative to execution prefix])
AC_SUBST(DVBSTREAMER_PLUGINDIR)

AC_OUTPUT([
Makefile
include/Makefile
include/dvbpsi/Makefile
src/dvbpsi/Makefile
src/Makefile
src/plugins/Makefile
])

